plugins {
    id 'base'
    id 'java'
}

group 'com.keepersecurity'
version '0.1.0'

base {
    archivesName = 'keeper-external-credentials'
}
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// TODO: replace with mid server path
// This must point to the MID Server installation location (agent directory path).
def midserver_agent_dir = '/opt/servicenow/mid/agent/lib'

repositories {
    mavenCentral()
    flatDir {
        dirs("$midserver_agent_dir")
    }
}

dependencies {
    implementation 'com.keepersecurity.secrets-manager:core:16.6.2+'

    // MID server dependencies, not required to be uploaded
    // MID jar dependency for config APIs
    compileOnly 'com.snc:mid'
    compileOnly 'com.snc:commons-glide'
    compileOnly 'com.snc:commons-core-automation'
    compileOnly 'com.snc:snc-automation-api'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes('Main-Class': 'com.snc.discovery.CredentialResolver')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}
